apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "us.mikeandwan.pictures"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 32
        versionName "4.3"

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        maw {
            if(new File(RELEASE_STORE_FILE).exists()) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    flavorDimensions "dev_or_prod"

    productFlavors {
        //noinspection GroovyMissingReturnStatement
        development {
            dimension "dev_or_prod"
        }

        //noinspection GroovyMissingReturnStatement
        production {
            dimension "dev_or_prod"
        }
    }

    buildTypes {
        release {
            // TODO: enable shrinkage
            // shrink is apparently not supported yet by jack
            // https://code.google.com/p/android/issues/detail?id=82168
            //shrinkResources true

            // TODO: enable minification, but am pulling my hair out with Jack
            //       app loads, but can't move to new activity, so disabling for now
            //minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.maw
        }
    }

    //noinspection GroovyMissingReturnStatement
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    // dagger 2
    compile 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'

    // butterknife
    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'

    // android support
    compile 'com.android.support:support-annotations:25.4.0'
    compile 'com.android.support:appcompat-v7:25.4.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:design:25.4.0'
    compile 'com.android.support:recyclerview-v7:25.4.0'

    // retrofit
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.3.0'

    // stream provider
    compile 'com.commonsware.cwac:provider:0.5.2'

    // picasso
    compile 'com.squareup.picasso:picasso:2.5.2'

    // io
    compile 'commons-io:commons-io:2.5'

    // reactivex
    compile "io.reactivex.rxjava2:rxjava:2.1.1"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // photoview
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
}
