apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "us.mikeandwan.pictures"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 38
        versionName "5.5"

        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = [
            'appAuthRedirectScheme': 'us.mikeandwan.photos'
        ]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        maw {
            if (new File(RELEASE_STORE_FILE).exists()) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    flavorDimensions "dev_or_prod"

    productFlavors {
        //noinspection GroovyMissingReturnStatement
        development {
            dimension "dev_or_prod"
        }

        //noinspection GroovyMissingReturnStatement
        production {
            dimension "dev_or_prod"
        }
    }

    buildTypes {
        debug {
            debuggable true
            javaCompileOptions.annotationProcessorOptions.argument('butterknife.debuggable', 'true')
        }
        release {
            // TODO:
            //shrinkResources true
            //minifyEnabled true

            debuggable false
            javaCompileOptions.annotationProcessorOptions.argument('butterknife.debuggable', 'false')
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.maw
        }
    }

    //noinspection GroovyMissingReturnStatement
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        //checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    // dagger 2
    implementation 'com.google.dagger:dagger:2.16'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.16'

    // butterknife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // android support
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'

    // appauth
    implementation 'net.openid:appauth:0.7.0'

    // ****
    // NOTE: retrofit 2.4 and okhttp 3.10 failed to work, so becareful when upgrading
    // ****
    
    // retrofit (pulls in okhttp)
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.3.0'

    // okhttp httpurlconnection (keep in sync w/ retrofit)
    //compile 'com.squareup.okhttp3.okhttp-urlconnection:3.8.0'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp-urlconnection', version: '3.8.0'

    // stream provider
    implementation 'com.commonsware.cwac:provider:0.5.3'

    // picasso
    implementation 'com.squareup.picasso:picasso:2.5.2'

    // io
    implementation 'commons-io:commons-io:2.5'

    // reactivex
    implementation "io.reactivex.rxjava2:rxjava:2.1.14"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    // photoview
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
}
