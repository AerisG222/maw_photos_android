apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "us.mikeandwan.pictures"
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 44
        versionName "6.1"

        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = [
            'appAuthRedirectScheme': 'us.mikeandwan.photos'
        ]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        maw {
            if (new File(RELEASE_STORE_FILE).exists()) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    flavorDimensions "dev_or_prod"

    productFlavors {
        //noinspection GroovyMissingReturnStatement
        development {
            dimension "dev_or_prod"
        }

        //noinspection GroovyMissingReturnStatement
        production {
            dimension "dev_or_prod"
        }
    }

    buildTypes {
        debug {
            debuggable true
            javaCompileOptions.annotationProcessorOptions.argument('butterknife.debuggable', 'true')
        }
        release {
            shrinkResources true
            minifyEnabled true

            debuggable false
            javaCompileOptions.annotationProcessorOptions.argument('butterknife.debuggable', 'false')
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.maw
        }
    }

    //noinspection GroovyMissingReturnStatement
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        //checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    // dagger 2
    implementation 'com.google.dagger:dagger:2.22.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.22.1'

    // butterknife
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'

    // android support
    implementation 'androidx.annotation:annotation:1.1.0-beta01'
    implementation 'androidx.appcompat:appcompat:1.1.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0-alpha05'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta01'

    // completable future
    implementation 'net.sourceforge.streamsupport:android-retrofuture:1.7.0'

    // appauth
    implementation 'net.openid:appauth:0.7.1'

    // retrofit (pulls in okhttp)
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.5.0'

    // stream provider
    implementation 'com.commonsware.cwac:provider:0.5.3'

    // picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    // io
    implementation 'commons-io:commons-io:2.6'

    // reactivex
    implementation "io.reactivex.rxjava2:rxjava:2.2.8"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // photoview
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
}
